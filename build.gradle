import org.apache.tools.ant.taskdefs.condition.Os
import groovy.json.JsonSlurper

version = new JsonSlurper().parse(file('package.json')).version
boolean snapshot = 'true'.equals(System.getProperty('build.snapshot', 'true'));
if (snapshot) {
  version += '-SNAPSHOT'
}

task clean(type: Delete) {
  group = 'Build'
  description = 'Delete all previously built output'
  delete 'build'
  delete 'node_modules'
}

task installDeps(type: Exec) {
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    commandLine 'cmd', '/C', 'yarn', '--frozen-lockfile'
  } else {
    commandLine 'yarn', '--frozen-lockfile'
  }
}

task mochaTests(type: Exec, dependsOn: installDeps) {
  group = 'Verification'
  description = 'Run mocha tests'
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    commandLine 'cmd', '/C', 'yarn', 'test'
  } else {
    commandLine 'xvfb-run', 'yarn', 'test'
  }
}

task jestTests(type: Exec, dependsOn: installDeps) {
  group = 'Verification'
  description = 'Run jest tests'
  commandLine 'node', 'scripts/jest', '--ci', '--no-cache', '--verbose'
}

task apiTests(type: Exec, dependsOn: installDeps) {
  group = 'Verification'
  description = 'Run api tests'
  commandLine 'node', 'scripts/functional_tests_api'
}

task functionalTests(type: Exec, dependsOn: installDeps) {
  group = 'Verification'
  description = 'Run functional tests'
  commandLine 'xvfb-run', 'node', 'scripts/functional_tests', '--bail'
}

task check(dependsOn: [mochaTests, jestTests, apiTests, functionalTests]) {
  jestTests.mustRunAfter mochaTests
  apiTests.mustRunAfter jestTests
  functionalTests.mustRunAfter apiTests
  group = 'Verification'
  description = 'Run all verification tasks'
}

task runBuild(type: Exec, dependsOn: installDeps) {
  if (Os.isFamily(Os.FAMILY_WINDOWS)) {
    commandLine 'cmd', '/C', 'yarn', 'build'
  } else {
    commandLine 'yarn', 'build'
  }
  if (System.getProperty('build.snapshot', 'true') == 'false') {
    args '--', '--release'
  }
}

task bundlePlugin(type: Zip, dependsOn: runBuild) {
  from 'build/plugin'
  destinationDir file('build/distributions')
  baseName = 'x-pack'
  version = project.version
}

task assemble(dependsOn: bundlePlugin) {
  group = 'Build'
  description = 'Assemble the x-pack kibana zip'
}

task build(dependsOn: [check, assemble]) {
  group = 'Build'
  description = 'Assembles and tests the kibana x-pack'
}
