
import org.elasticsearch.gradle.LoggedExec
import org.elasticsearch.gradle.VersionProperties

String enabledStr = properties.get('xpack.kibana.build', 'true')
if (['true', 'false'].contains(enabledStr) == false) {
  throw new GradleException("xpack.kibana.build must be true or false, got ${enabledStr}")
}
boolean kibanaEnabled = enabledStr == 'true'

task clean(type: Delete) {
  group = 'Build'
  description = 'Delete all previously built output'
  delete 'build'
  delete 'node_modules'
}

task npmInstall(type: LoggedExec) {
  enabled = kibanaEnabled
  commandLine 'npm', 'install'
}

task test(type: LoggedExec, dependsOn: npmInstall) {
  enabled = kibanaEnabled
  group = 'Verification'
  description = 'Run npm tests'
  commandLine 'npm', 'run', 'test'
}

task check(dependsOn: test) {
  group = 'Verification'
  description = 'Run all verification tasks'
}

task npmBuild(type: LoggedExec, dependsOn: npmInstall) {
  enabled = kibanaEnabled
  commandLine 'npm', 'run', 'build'
}

task npmPackage(type: LoggedExec, dependsOn: npmBuild) {
  enabled = kibanaEnabled
  commandLine 'npm', 'run', 'package'
  outputs.file "target/x-pack-${VersionProperties.elasticsearch}.zip"
}

task assemble(dependsOn: npmPackage) {
  group = 'Build'
  description = 'Assemble the x-pack kibana zip'
}

task build(dependsOn: [check, assemble]) {
  group = 'Build'
  description = 'Assembles and tests the kibana x-pack'
}
